plugins {
    id "com.android.application"
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

android {
    namespace = "org.soralis.suica.suica_viewer"
    compileSdk = 36
    ndkVersion = "26.3.11579264"

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId = "org.soralis.suica.suica_viewer"
        minSdkVersion = flutter.minSdkVersion
        targetSdk = 36
        versionCode = flutterVersionCode.toInteger()
        versionName = flutterVersionName
    }
    signingConfigs {
        // Local/dev keystore shipped in repository (android.jks)
        android {
            storeFile rootProject.file('android.jks')
            storePassword 'android'
            keyAlias 'android'
            keyPassword 'android'
        }

        // Release keystore optionally provided as android/app/release.jks
        // and credentials via environment variables (CI-friendly).
        release {
            storeFile file('release.jks')
            storePassword System.getenv('STORE_PASSWORD')
            keyAlias System.getenv('KEY_ALIAS')
            keyPassword System.getenv('KEY_PASSWORD')
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.android
        }
        release {
            // Prefer release keystore if present; otherwise fall back to android (dev) keystore
            if (file('release.jks').exists()) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.android
            }
        }
    }
}

flutter {
    source = "../.."
}
